plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "ma.dexter"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'org/antlr/codegen/templates/**' // useless templates taking up +1MB
        exclude 'org/antlr/v4/tool/templates/codegen/**'
        exclude 'com/googlecode/dex2jar/tools/**' // useless debug keys
        exclude 'smali.properties'
        exclude 'baksmali.properties'
        exclude 'export/build.gradle.tmpl' // from jadx
    }
}

tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // AndroidX
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.fragment:fragment-ktx:1.3.6"

    // For unit tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'org.eclipse.jdt.core.compiler:ecj:4.6.1'
    testImplementation 'com.android.tools:r8:3.0.73'

    implementation 'io.github.Rosemoe.sora-editor:editor:0.8.3'
    implementation 'io.github.Rosemoe.sora-editor:language-base:0.8.3'
    implementation 'io.github.Rosemoe.sora-editor:language-universal:0.8.3'
    implementation 'io.github.Rosemoe.sora-editor:language-java:0.8.3'
    implementation 'com.github.angads25:filepicker:1.1.1'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'
    implementation 'com.github.zawadz88.materialpopupmenu:material-popup-menu:4.1.0'
    implementation 'com.google.guava:guava:27.1-android'

    // dex2jar (maintained fork)
    def d2jVersion = "v34"
    def withoutIcu = { exclude group: 'com.ibm.icu', module: 'icu4j' } // Adds ~10MB to the APK !
    implementation "com.github.ThexXTURBOXx.dex2jar:dex-tools:$d2jVersion", withoutIcu
    implementation "com.github.ThexXTURBOXx.dex2jar:dex-translator:$d2jVersion", withoutIcu

    // smali & baksmali
    implementation "org.smali:smali:2.5.2"
    implementation "org.smali:baksmali:2.5.2"

    // fernflower (mirror)
    // doesn't have a proper versioning so we're stuck with using this
    implementation 'com.github.fesh0r:fernflower:e19aab61ce'

    // cfr
    implementation 'org.benf:cfr:0.151'

    // jd-core
    /*
     * jd-core's repo fixed some critical bugs in their commits
     * since the last release (v1.1.3), that's why we're using the
     * hash of the latest commit (might fork and release myself later)
     */
    implementation 'com.github.java-decompiler:jd-core:fafc656826'

    // procyon
    implementation 'com.github.MikeAndrson.procyon:procyon-compilertools:androidport_0.5.36'
    implementation files("libs/procyon/javax-model.jar") // for javax.lang.model

    // jadx
    implementation files("libs/jadx/jadx-core-v1.2.0.jar")
    implementation files("libs/jadx/jadx-plugins-api-v1.2.0.jar")
    implementation files("libs/jadx/jadx-dex-input-v1.2.0.jar")
    implementation files("libs/jadx/android-29-clst.jar")
    implementation 'org.slf4j:slf4j-api:1.7.32' // 1.7.30
}
